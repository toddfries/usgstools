#!/usr/bin/perl

# Copyright (c) 2014 Todd T. Fries <todd@fries.net>
#
# Permission to use, copy, modify, and distribute this software for any
# purpose with or without fee is hereby granted, provided that the above
# copyright notice and this permission notice appear in all copies.
#
# THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
# WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
# ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
# WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
# ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
# OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.

use strict;
use warnings;

use JSON;
use Math::Trig;
use POSIX qw(strftime);
use URI;
use WWW::Mechanize;

use OpenBSD::Getopt;

my $mech = WWW::Mechanize->new(stack_depth => 0, quiet => 0);

# select your details: http://earthquake.usgs.gov/earthquakes/feed/v1.0/geojson.php

my $uri;
#$uri = URI->new("http://earthquake.usgs.gov/earthquakes/feed/v1.0/summary/1.0_day.geojson");
$uri = URI->new("http://earthquake.usgs.gov/earthquakes/feed/v1.0/summary/1.0_week.geojson");
#$uri = URI->new("http://earthquake.usgs.gov/earthquakes/feed/v1.0/summary/1.0_month.geojson");
#$uri = URI->new("http://earthquake.usgs.gov/earthquakes/feed/v1.0/summary/4.5_month.geojson");

#use URI::file;
#$uri = URI::file->new("$ENV{'HOME'}/git/sw/usgstools/1.0_day.geojson");

my $req = HTTP::Request->new( 'GET', $uri );

eval {
	$mech->request($req);
};
if ($@) {
	printf STDERR "request failure: %s\n", $@;
	exit(1);
}

my $json = JSON->new->allow_nonref;

my $info = $json->decode( $mech->content );

if (!defined($info)) {
	print STDERR "No eq info retrieved..\n";
	exit(1);
}

my $coll = $info->{features};

if (!defined($coll)) {
	print STDERR "No Collection retrieved...\n";
	exit(1);
}
our $opt_a;     # Lattitude
our $opt_o;     # Longitude
our $opt_v = 0; # verbose

getopts('a:o:v');

my $regex = $ARGV[0];

my $lat = $opt_a;
my $lon = $opt_o;

printf STDOUT "%24s|%10s|%4s|%9s|%s\n", "Date", "Eq Id", "Mag", "From Home",
    "Location";

foreach my $eq (@{$coll}) {
	my $place = $eq->{properties}->{place};
	if (defined($regex)) {
		if (! ($place =~ /$regex/)) {
			next;
		}
	}
	if (!defined($place)) {
		next;
	}
	my $mag = $eq->{properties}->{mag};
	my $time = $eq->{properties}->{time};
	$time *= .001;
	$time = strftime "%a %b %e %H:%M:%S %Y", localtime($time);

	my $geom = $eq->{geometry}->{coordinates};
	my ($eqlon,$eqlat,$eqdepth) = @{$geom};


	my $distance = sprintf "%0.2fmi", distance($lat, $lon, $eqlat, $eqlon);
	$place =~ m/^(.*[[:space:]]*)([0-9]+)km(.*)$/;
	my ($plpre,$plkm,$plpost) = ($1,$2,$3);
	if (defined($plkm)) {
		if (!defined($plpre)) {
			$plpre="";
		}
		my $plmi = $plkm * 0.63;
		$place = sprintf "%6.2fmi%s", $plpre.$plmi, $plpost;
	}

	my $id = $eq->{id};
	
	printf STDOUT "%s|%9s|%4.1f|%9s|%s\n",
	    $time, $id, $mag, $distance, $place;
	if ($opt_v > 0) {
		print STDERR "       ";
		dumpit($eq, '       ',' ');
	}
}

sub dumpit
{
	my ($info, $indent, $oindent) = @_;

	my $ai = "    ";

	if (!defined($indent)) {
		$indent = "";
	}
	if (!defined($oindent)) {
		$oindent = "";
	}
	if (!defined($info)) {
		$info = "<undef>";
	}

	my $type = ref($info);

	unless ($type) {
		printf STDERR " %s\n", $info;
		return;
	}

	if ($type eq "ARRAY") {
		printf STDERR "ARRAY (\n";
		foreach my $a (@{$info}) {
			print STDERR $indent;
			dumpit($a, $indent.$ai, $indent);
		}
		printf STDERR "%s),\n", substr($indent,0,length($indent)-5);;
		return;
	}
	if ($type eq "HASH") {
		print STDERR "HASH {";
		my @keylist = keys %{$info};
		if (! @keylist || $#keylist < 0) {
			print STDERR " <empty> }\n";
			return;
		}
		print STDERR "\n";
		foreach my $k (keys %{$info}) {
			printf STDERR "%s '%s' => ", $indent.$ai, $k;
			dumpit($info->{$k}, $indent.$ai.$ai, $indent);
		}
		printf STDERR "%s},\n", substr($indent,0,length($indent)-5);;
		return;
	}
	use Data::Dumper;
	$Data::Dumper::Purity = 1;
	$Data::Dumper::Pad = $indent;
	$Data::Dumper::Deepcopy = 1;
	$Data::Dumper::Deparse = 1;
	print STDERR Data::Dumper->Dump([$type], [qw(type)]);
	return;
}
sub
distance
{
	my ($lat1, $long1, $lat2, $long2, $unit) = @_;
	my $theta = $long1 - $long2;
	my $dist = sin(deg2rad($lat1)) * sin(deg2rad($lat2)) + cos(deg2rad($lat1)) * cos(deg2rad($lat2)) * cos(deg2rad($theta));
	$dist = acos($dist);
	$dist = rad2deg($dist);
	$dist = $dist * 60 * 1.1515;
	if (!defined($unit)) {
		$unit = "";
	}
	if ($unit eq "K") {
		$dist = $dist * 1.609344;
	} elsif ($unit eq "N") {
		$dist = $dist * 0.8684;
	}
	return ($dist);
}
